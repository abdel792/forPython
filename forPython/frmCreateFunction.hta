<HTML>
	<HEAD> 
		<TITLE>Nouvelle fonction</TITLE>
		<HTA:APPLICATION ID='Document1' APPLICATIONNAME='Document1' CAPTION='yes' CONTEXTMENU='yes' MAXIMIZEBUTTON='yes' MINIMIZEBUTTON='yes' NAVIGABLE='yes' SELECTION='yes' SHOWINTASKBAR='yes' SYSMENU='yes' WindowState='normal' >

			<script language="VBScript">

			Sub Window_onLoad
' se lance au démarrage de la fenêtre
document.form1.funcName.focus()
			End Sub

sub form1_validate()
' à la validation du formulaire
' on va généré le texte à écrire dans le fichier ini intermédiaire
dim s: s = ""
dim v: v = ""
on error resume next
' le nom de la fonction
v = document.form1.funcName.value
if v = "" then
MsgBox "Vous devez obligatoirement taper le nom de la fonction avant d'effectuer cette action.", vbOKOnly+vbExclamation, "Erreur- nom de la fonction manquant" 
document.form1.funcName.focus()
exit sub
end if
s = s & "funcName=" & v & vbcrlf
' le type de la fonction
v = document.form1.funcType.value
if v = "" then
MsgBox "Vous devez obligatoirement choisir le type de la fonction avant d'effectuer cette action.", vbOKOnly+vbExclamation, "Erreur- type de la fonction manquant" 
document.form1.funcType.focus()
exit sub
end if
s = s & "funcType=" & v & vbcrlf
' si self
v = abs(document.form1.self.checked)
s = s & "self=" & v & vbcrlf
' les paramètres de la fonction
v = formatResult(document.form1.params.value)
s = s & "params=" & v & vbcrlf
' la description de la fonction
v = formatResult(document.form1.description.value)
s = s & "description=" & v & vbcrlf
' on trouve le chemin vers le fichier ini intermédiaire
sPath = document.location.pathname
tbl = split(sPath, "\")
redim preserve tbl(ubound(tbl)-1)
sPath = join(tbl, "\")
sPath = sPath & "\" & "com.ini"
writeFile sPath, s, true
window.close
end sub

sub form1_cancel()
' bouton annuler
window.close
End sub

function readFile(sPath)
' lit et renvoi le contenu d'un fichier
s=""
on error resume next
set fso=CreateObject("Scripting.FileSystemObject")
set fichier=fso.GetFile(sPath)
set ts=fichier.OpenAsTextStream
' ForReading
s=ts.ReadAll
ts.Close
set ts=nothing
set fichier=nothing
set fso=nothing
readFile = s
end function

Function WriteFile (sFilePath, sText, BlErase)
'' écriture dans un fichier
on error resume next
set fs = CreateObject("Scripting.FileSystemObject")
'
' si on doit écraser ou pas
if BlErase = true then
Set FSStream = fs.CreateTextFile(sFilePath, True)
FSStream.Write (sText) 
FSStream.Close
else ' il s'agit d'un ajout au fichier
Set f = FS.GetFile(sFilePath)
Set FSStream = f.OpenAsTextStream(8)
FSStream.WriteLine sText
FSStream.Close
end if ' fin si BlErase
Set FSStream = Nothing
set f = nothing
set FS = Nothing
End Function

function FormatResult(s)
'
dim s2: s2 = ""
dim reg
set reg = CreateObject("vbscript.regexp")
reg.Global = true
reg.IgnoreCase = true
reg.Multiline = false
reg.Pattern = "[^\r\n]+"
set found = reg.Execute(s)
if found.count > 0 then
for i = 0 to found.count-1
if i > 0 then s2 = s2 & ",,,"
s2 = s2 & found(i).value
next
end if
set found = nothing
set reg = nothing
' renvoi
FormatResult = s2
end function

		</script>
	</HEAD>
	<BODY>

<form name="form1" onSubmit="form1_validate">

<label for="funcName">Nom de la fonction:</label>
<input type="text" name="funcName" id="funcName" />

<br />

<label for="description">Description du rôle de la fonction</label>
<textarea name="description" id="description" rows="3" cols="40"></textarea>

<br />

<p>
<label for="funcType">Type:</label>
<select name="funcType" id="funcType">
<option value="def">def</option>
<option value="property">property</option>
</select>
</p>

<br />

<p>
self ?
<input type="checkbox" name="self" id="self" />
<label for="self">self ?</label>
</p>

<br />

<label for="params">Liste des paramètres de la fonction:</label>
<textarea name="params" id="params" rows="5" cols="40"></textarea>

<br />

<INPUT Type="button" Value="Créer" onClick="form1_validate" />

<INPUT Type="button" Value="Annuler" onClick="form1_cancel" />

</form>

	</BODY>
</HTML>










